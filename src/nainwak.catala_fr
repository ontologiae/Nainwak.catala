# Code Catala pour une version hyper simplifiée du MMPORG Nainwak
> Module Nainwak


```catala-metadata
# énumération = un Cote peut être l'un des 3, rien d'autre
déclaration énumération Cote:
  -- Brave
  -- Sadique
  -- Naindécis


déclaration énumération TypeArme:
	-- Snipe
	-- Bourrin



déclaration structure Carac:
  donnée force        contenu entier   # PF
  donnée precision    contenu entier   # PP
  donnée intelligence contenu entier   # PI
  donnée barbe        contenu entier  # longueur de la barbe (cm)
  donnée pv          contenu entier   # Points de Vie actuels
  donnée pv_max      contenu entier   # PV maximum
  donnée pa          contenu entier   # Points d'Action disponibles
  donnée xp          contenu entier   # Points d'expérience
  donnée honor       contenu entier   # Points d'honneur
  donnée points_cote contenu entier   # Points de côté (PC)
  donnée ridicule    contenu entier   # Points de ridicule (PR)
  donnée honte       contenu entier   # Points de honte (PH)
  donnée encyclo    contenu entier    # Points d'encyclopédie

déclaration structure Arme:
  donnée coût_pa      contenu entier   # coût en PA pour l'utiliser
  donnée portée        contenu entier   # portée (nombre de cases)
  donnée base_dommage  contenu entier   # dommage de base
  donnée recharge     contenu entier  # temps de recharge (jours)
  donnée état_arme        contenu entier   # état de l'arme (0 = cassée)
  donnée type_arme         contenu TypeArme

déclaration structure Rune:
  donnée delta_force       contenu entier
  donnée delta_precision   contenu entier
  donnée delta_vie         contenu entier
  donnée delta_intelligence contenu entier

déclaration structure Véhicule:
  donnée id 	contenu entier   # ID unique du véhicule
  donnée coût_pa_par_case contenu entier   # coût PA d'un déplacement d'une case
  donnée état_vh             contenu entier   # état du véhicule
  donnée peut_glisser_avec  contenu booléen 
  donnée cout_a_lexplosion contenu entier   # coût en PV quand il explose 
  donnée cout_utilisation   contenu entier   # coût en % pour l'utiliser

déclaration structure Détecteur:
  donnée portée contenu entier   # portée de détection (unités : cases)

déclaration structure Bouffe:
  donnée heal  contenu entier   # points de vie restaurés

déclaration structure Jouet:
  donnée dummy contenu entier   # placeholder (pas d'effet défini)

déclaration structure Inventaire:
  donnée armes      contenu liste de Arme
  donnée runes      contenu liste de Rune
  donnée véhicule   contenu optionnel de Véhicule
  donnée détecteur  contenu optionnel de Détecteur
  donnée aliments   contenu liste de Bouffe
  donnée inutiles   contenu liste de entier   # objets inutiles (ID)
  donnée jouets     contenu liste de Jouet

déclaration structure Personnage:
  donnée id 	    contenu entier   # ID unique du nain
  donnée carac      contenu Carac
  donnée inventaire contenu Inventaire
  #donnée target     contenu optionnel de entier   # ID du nain cible (si présent)
  donnée x          contenu entier   # position x sur la carte	
  donnée y          contenu entier   # position y sur la carte
  donnée monde      contenu entier   # ID du monde actuel

déclaration champ d'application Deplacement:
  entrée personnage_in      contenu Personnage
  entrée direction_x    contenu entier   # -1, 0, ou 1
  entrée direction_y    contenu entier   # -1, 0, ou 1
  interne deplacement_autorisé     contenu booléen
  interne déplacement_à_pied   contenu booléen
  interne cout_pa      contenu entier
  interne a_explosé    contenu booléen
  résultat est_mort    contenu booléen
  résultat déplacement_possible condition
  résultat explosion    contenu (booléen, entier)
  résultat personnage_out contenu Personnage
```


Définition du déplacement du joueur


```catala
champ d'application Deplacement:
  étiquette pas_de_deplacement définition personnage_out égal à personnage_in # par défaut
  # coût en PA selon la présence d'un véhicule 



champ d'application Deplacement:

 définition cout_pa égal à
   selon (personnage_in.inventaire.véhicule) sous forme
      -- Absent: 8
      -- Présent contenu veh: veh.coût_pa_par_case

 définition déplacement_à_pied égal à 
    (personnage_in.inventaire.véhicule = Absent)
 #TODO : gérer l'explosion

 définition explosion égal à 
    selon (personnage_in.inventaire.véhicule) sous forme
      -- Absent: (faux,0)
      -- Présent contenu veh: ((veh.état_vh - veh.cout_utilisation <= 0),veh.cout_a_lexplosion)

 définition deplacement_autorisé égal à 
    soit position_ok égal à 
    (direction_x != 0 ou direction_y != 0) 
	et ( personnage_in.x + direction_x <= 22 et personnage_in.x + direction_x >= 0) 
	et (personnage_in.y + direction_y <= 8 et personnage_in.y + direction_y >= 0)
    dans soit assez_pa égal à personnage_in.carac.pa >= cout_pa
    dans  (assez_pa et position_ok)

  

  # Cas déplacement à pied
  étiquette cas_deplacement_à_pied exception pas_de_deplacement définition personnage_out sous condition (deplacement_autorisé et déplacement_à_pied)
	conséquence égal à 
       		personnage_in mais en remplaçant {
        		-- carac : personnage_in.carac mais en remplaçant { 
									  -- pa : personnage_in.carac.pa - cout_pa 
									  }
			-- x : personnage_in.x + direction_x 
			-- y : personnage_in.y + direction_y
     		}

   définition a_explosé égal à 
	soit (boom, dmg) égal à explosion 
	dans boom


   définition est_mort égal à
	soit (boom, dmg) égal à explosion dans
	(personnage_in.carac.pv - dmg <= 0)

  # Cas déplacement avec véhicule
   étiquette cas_deplacement_avec_véhicule exception cas_deplacement_à_pied
	définition personnage_out sous condition (deplacement_autorisé et non déplacement_à_pied et non a_explosé et non est_mort)
	conséquence égal à
		soit x égal à #[debug.print = "cas_deplacement_avec_véhicule"] 0 dans
		soit newvh égal à 
		 selon (personnage_in.inventaire.véhicule) sous forme
	 		-- Absent: personnage_in.inventaire.véhicule
	 		-- Présent contenu veh: Présent contenu (veh mais en remplaçant {
	      				-- état_vh : veh.état_vh - veh.cout_utilisation
	      			})
		dans
		#soit (boom, dmg) = pas_d_explosion
       		personnage_in mais en remplaçant {
			-- carac : personnage_in.carac  mais en remplaçant { 
									  -- pa : personnage_in.carac.pa - cout_pa 
									}
			-- x : personnage_in.x + direction_x 
			-- y : personnage_in.y + direction_y
			-- inventaire : personnage_in.inventaire mais en remplaçant {
							-- véhicule : newvh
     		 						}
		}

   étiquette explosion_vehicule_sans_mort exception cas_deplacement_avec_véhicule
	définition personnage_out sous condition (deplacement_autorisé et a_explosé et non déplacement_à_pied et non est_mort)
	conséquence égal à
	soit x égal à #[debug.print = "explosion_vehicule_sans_mort"] 0 dans

		 soit (explo,coupv) égal à explosion dans 
       		 personnage_in mais en remplaçant {
			-- carac : personnage_in.carac  mais en remplaçant { 
									  -- pa : personnage_in.carac.pa - cout_pa
									  -- pv : personnage_in.carac.pv - coupv
									}
			-- x : personnage_in.x + direction_x # le déplacement a quand même eu lieu...
			-- y : personnage_in.y + direction_y
			-- inventaire : personnage_in.inventaire mais en remplaçant {
							-- véhicule : Absent # mais le véhicule a explosé
     		 						}
		}

    étiquette  explosion_vehicule_avec_mort exception explosion_vehicule_sans_mort
	définition personnage_out sous condition (deplacement_autorisé et non déplacement_à_pied et a_explosé et est_mort)
	conséquence égal à
	soit x égal à  #[debug.print = "explosion_vehicule_avec_mort"] 0 dans
       		 personnage_in mais en remplaçant {
			-- carac : personnage_in.carac  mais en remplaçant { 
									  -- pa : personnage_in.carac.pa - cout_pa
									  -- pv : personnage_in.carac.pv_max # le personnage meurt, il respawn avec ses PV max
									  # nb mort + 1
									  # On voiti ci les limites de Catala, faut MAJ les stats, ailleurs dans le jeu, respawn le nain, etc.
									}
			-- x : 0 # impossible, mais le moteur s'en occupera après
			-- y : 0
			-- inventaire : Inventaire { 	
					-- armes : []
					-- runes : []
					-- véhicule : Absent
					-- détecteur : Absent
					-- aliments : []
					-- inutiles : []
					-- jouets : []
					}
     		 }
   


   règle déplacement_possible sous condition deplacement_autorisé conséquence rempli  
```


